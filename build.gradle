plugins {
	id 'io.github.ladysnake.chenille' version '0.9.0'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

chenille {
	configurePublishing {
		withArtifactory()
		withCurseforgeRelease()
		withGithubRelease()
		withModrinthRelease()
	}
	configureTestmod {
		withDependencyConfiguration()
	}

	javaVersion = 17
}

modrinth {
	loaders = ["fabric"]
}

repositories {
	mavenLocal()
	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
	maven {
		name = 'Ladysnake Mods'
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
	}
	maven {
		url = 'https://maven.jamieswhiteshirt.com/libs-release/'
		content {
			includeGroup 'com.jamieswhiteshirt'
		}
	}
	maven {
		url = 'https://oss.sonatype.org/content/repositories/snapshots'
		content {
			includeGroup 'me.lucko'
		}
	}
	maven {
		name "fall flying lib"
		url "https://maven.cafeteria.dev"
	}
	maven {
		name "cloth config"
		url "https://maven.shedaniel.me/"
	}
	maven {
		name "mod menu"
		url "https://maven.terraformersmc.com/"
	}
	maven {
		name "modrinth"
		url "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url = 'https://jitpack.io'
		content {
			includeGroupByRegex '(com|io)\\.github\\..*'
		}
	}
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings(loom.layered {
		addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${quilt_mappings}:v2"))
	})
	modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"
	modImplementation "org.quiltmc:qsl:${qsl_version}+${minecraft_version}"
	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilted_fabric_api_version}-${minecraft_version}"

	// Geckolib
	modImplementation "software.bernie.geckolib:geckolib-fabric-${project.geckolib_version}"

	// Origins + Requiem (no need to remove these every update)
	modCompileOnly ("com.github.apace100:apoli:${project.apoli_version}") { transitive = false }
	modCompileOnly ("com.github.apace100:calio:${project.calio_version}") { transitive = false }
	modCompileOnly ("maven.modrinth:origins:${project.origins_version}") { transitive = false }
	modCompileOnly ("io.github.ladysnake:PlayerAbilityLib:${project.pal_version}")
	modCompileOnly ("io.github.ladysnake:requiem-api:${project.requiem_version}")
	modCompileOnly ("io.github.ladysnake:requiem-core:${project.requiem_version}") { transitive = false }
	modCompileOnly ("io.github.ladysnake:requiem:${project.requiem_version}") { transitive = false }
	modCompileOnly modLocalRuntime ("io.github.ladysnake:elmendorf:0.8.0")

	runtimeOnly "me.zeroeightsix:fiber:0.23.0-2"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}
