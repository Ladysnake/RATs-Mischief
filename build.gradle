plugins {
	id 'maven-publish'
	id 'io.github.ladysnake.chenille' version '0.11.3'
}

archivesBaseName = project.archives_base_name
version = project.version
group = project.maven_group

repositories {
	mavenLocal()
	// publishing
	maven {
		name = "CurseForge"
		url = "https://minecraft.curseforge.com/api/maven"
	}

	// cloth config
	maven { url "https://maven.shedaniel.me/" }

	// CCA, Requiem
	chenille.repositories.ladysnake()

	// mod menu
	maven {
		name = 'TerraformersMC'
		url = 'https://maven.terraformersmc.com/'
	 }

	maven { url 'https://jitpack.io' }

	maven {
		name "modrinth"
		url "https://api.modrinth.com/maven"
	}

	maven { url 'https://masa.dy.fi/maven' }

	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }

	// mialee misc
	maven { url "https://repo.wawwior.me/repository/maven-public/" }

	maven { url "https://maven.willbl.dev/releases" }

	maven {
		url = 'https://maven.jamieswhiteshirt.com/libs-release/'
		content {
			includeGroup 'com.jamieswhiteshirt'
		}
	}

	maven {
		url = 'https://oss.sonatype.org/content/repositories/snapshots'
		content {
			includeGroup 'me.lucko'
		}
	}

	// ears
	maven { url "https://repo.unascribed.com" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "org.quiltmc:quilt-mappings:${minecraft_version}+build.${quilt_mappings}:intermediary-v2"

	modImplementation "software.bernie.geckolib:${project.geckolib_version}"

	modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"
	modImplementation "org.quiltmc:qsl:${qsl_version}+${minecraft_version}"

	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilted_fabric_api_version}-${minecraft_version}"

	// reach attributes
	modImplementation("com.github.emilyploszaj:step-height-entity-attribute:v1.0.1")
	include("com.github.emilyploszaj:step-height-entity-attribute:v1.0.1")

	// mod menu
	modLocalImplementation("com.terraformersmc:modmenu:${mod_menu_version}") { transitive = false }

	// mixin extras
	implementation include("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}")
	annotationProcessor("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}")

	// mialee misc
	modImplementation include("xyz.amymialee:mialeemisc:${project.mialeemisc_version}")

	// elegant armor
    modLocalImplementation("xyz.amymialee:elegantarmour:${project.elegantarmour_version}") { transitive = false }

	// ok zoomer
//	runtimeOnly("com.github.EnnuiL:OkZoomer:1.19.2-SNAPSHOT")

	modCompileOnly("io.github.ladysnake:PlayerAbilityLib:${project.pal_version}")
	modImplementation("io.github.ladysnake:requiem-api:${project.requiem_version}")
	modImplementation("io.github.ladysnake:requiem-core:${project.requiem_version}")
	modImplementation include("io.github.ladysnake:requiem-lite:${project.requiem_version}")

	// trinkets
	modImplementation ("dev.emi:trinkets:${project.trinkets_version}")
}

chenille {
	javaVersion = 17
	configurePublishing {
		mainArtifact = remapJar.archiveFile
		withLadysnakeMaven()
		withCurseforgeRelease()
		withGithubRelease()
		withModrinthRelease()
	}
}

processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

java {
	withSourcesJar()
}

// If you plan to use a different file for the license, don't forget to change the file name here!
jar {
	from('LICENSE') {
		rename { "${it}_${archivesBaseName}" }
	}
}
